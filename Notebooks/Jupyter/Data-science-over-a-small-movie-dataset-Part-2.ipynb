{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "938ca051",
   "metadata": {},
   "source": [
    "# Data science over small movie dataset -- Part 2\n",
    "\n",
    "<p style=\"font-size: 20px; font-weight: bold;\">Sparse matrix recommender</p>\n",
    "\n",
    "Anton Antonov   \n",
    "October 2025  \n",
    "November 2025  "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "229baefa",
   "metadata": {},
   "source": [
    "---\n",
    "\n",
    "## Introduction"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c6975240",
   "metadata": {},
   "source": [
    "This notebook shows transformation of movie dataset into a form more suitable for making a movie recommender system. "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "24e3d6dc",
   "metadata": {},
   "source": [
    "The movie data was downloaded from here: [\"IMDB Movie Ratings Dataset\"](https://www.kaggle.com/datasets/thedevastator/imdb-movie-ratings-dataset). That dataset was chosen because:\n",
    "\n",
    "- It has the right size for demonstration of data wrangling techniques\n",
    "    - ≈5000 rows and 15 columns (each row corresponding to a movie)\n",
    "- It is \"real life\" data with expected skewness of variable distributions\n",
    "- It is diverse enough over movie years and genres\n",
    "- There are no missing values"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7efdf68c",
   "metadata": {},
   "source": [
    "---\n",
    "\n",
    "## Setup"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8495d51b",
   "metadata": {},
   "source": [
    "Load packages used in the notebook:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d8cb78c4",
   "metadata": {},
   "outputs": [],
   "source": [
    "use Math::SparseMatrix;\n",
    "use ML::SparseMatrixRecommender;\n",
    "use ML::SparseMatrixRecommender::Utilities;\n",
    "use Statistics::OutlierIdentifiers;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "62aa7be7",
   "metadata": {},
   "outputs": [],
   "source": [
    "#% javascript\n",
    "require.config({\n",
    "     paths: {\n",
    "     d3: 'https://d3js.org/d3.v7.min'\n",
    "}});\n",
    "\n",
    "require(['d3'], function(d3) {\n",
    "     console.log(d3);\n",
    "});"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2202b4ba",
   "metadata": {},
   "outputs": [],
   "source": [
    "#% js\n",
    "js-d3-list-line-plot(10.rand xx 40, background => 'none', stroke-width => 2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e9cad556",
   "metadata": {},
   "outputs": [],
   "source": [
    "my $title-color = 'Silver';\n",
    "my $stroke-color = 'SlateGray';\n",
    "my $tooltip-color = 'LightBlue';\n",
    "my $tooltip-background-color = 'none';\n",
    "my $tick-labels-font-size = 10;\n",
    "my $tick-labels-color = 'Silver';\n",
    "my $tick-labels-font-family = 'Helvetica';\n",
    "my $background = '#1F1F1F';\n",
    "my $color-scheme = 'schemeTableau10';\n",
    "my $color-palette = 'Inferno';\n",
    "my $edge-thickness = 3;\n",
    "my $vertex-size = 6;\n",
    "my $mmd-theme = q:to/END/;\n",
    "%%{\n",
    "  init: {\n",
    "    'theme': 'forest',\n",
    "    'themeVariables': {\n",
    "      'lineColor': 'Ivory'\n",
    "    }\n",
    "  }\n",
    "}%%\n",
    "END\n",
    "my %force = collision => {iterations => 0, radius => 10},link => {distance => 180};\n",
    "my %force2 = charge => {strength => -30, iterations => 4}, collision => {radius => 50, iterations => 4}, link => {distance => 30};\n",
    "\n",
    "my %opts = :$background, :$title-color, :$edge-thickness, :$vertex-size;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "31759e4d",
   "metadata": {},
   "source": [
    "---\n",
    "\n",
    "## Ingest transformed data"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "70dfa2da",
   "metadata": {},
   "source": [
    "Ingest the movie data already ***transformed*** in the first notebook, [AAn1]:"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "30c30c8f",
   "metadata": {},
   "source": [
    "----\n",
    "\n",
    "## Recommender system"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "3792474c",
   "metadata": {},
   "source": [
    "One way to investigate (browse) the data is to make a recommender system and explore with it different aspects of the movie dataset like movie profiles and nearest neighbors similarities distribution."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "15899cc8",
   "metadata": {},
   "source": [
    "### Make the recommender"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d682d5c2",
   "metadata": {},
   "source": [
    "In order to make a more meaningful recommender we put the values of the different numerical variables into \"buckets\" -- i.e. intervals derived corresponding to the values distribution for each variable. The boundaries of the intervals can form a regular grid, correspond to quanitile values, or be specially made. Here we use quantiles:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b57f4873",
   "metadata": {},
   "outputs": [],
   "source": [
    "my @bucketVars = <score votes_count reviews_count>;\n",
    "my @dsMovieDataLongForm2;\n",
    "sink for @dsMovieDataLongForm.map(*<TagType>).unique -> $var {\n",
    "    if $var ∈ @bucketVars {\n",
    "        my %bucketizer = ML::SparseMatrixRecommender::Utilities::categorize-to-intervals(@dsMovieDataLongForm.grep(*<TagType> eq $var).map(*<Tag>)».Numeric, probs => (0..6) >>/>> 6, :interval-names):pairs;\n",
    "        @dsMovieDataLongForm2.append(@dsMovieDataLongForm.grep(*<TagType> eq $var).map(*.clone).map({ $_<Tag> = %bucketizer{$_<Tag>}; $_ }))\n",
    "    } else {\n",
    "        @dsMovieDataLongForm2.append(@dsMovieDataLongForm.grep(*<TagType> eq $var))\n",
    "    }\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a181a37b",
   "metadata": {},
   "outputs": [],
   "source": [
    "sink records-summary(@dsMovieDataLongForm2)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f9684b64",
   "metadata": {},
   "source": [
    "Here we make a Sparse Matrix Recommender (SMR):"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "baebd10b",
   "metadata": {},
   "outputs": [],
   "source": [
    "my $smrObj = \n",
    "    ML::SparseMatrixRecommender.new\n",
    "    .create-from-long-form(\n",
    "        @dsMovieDataLongForm2.append(@dsMovieGenreLongForm), \n",
    "        item-column-name => 'Item', \n",
    "        tag-type-column-name => 'TagType',\n",
    "        tag-column-name => 'Tag',\n",
    "        :add-tag-types-to-column-names)        \n",
    "    .apply-term-weight-functions('IDF', 'None', 'Cosine')"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "822ae9f1",
   "metadata": {},
   "source": [
    "Here are the recommender sub-matrices dimensions (rows and columns):"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "192c2780",
   "metadata": {},
   "outputs": [],
   "source": [
    ".say for $smrObj.take-matrices.deepmap(*.dimensions).sort(*.key)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9bc2de79",
   "metadata": {},
   "source": [
    "Note that the sub-matrices of \"reviews_count\", \"score\", and \"votes_count\" have small number of columns, corresponding to the number probabilities specified when categorizing to intervals."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cf197248",
   "metadata": {},
   "source": [
    "### Enhance one-hot embedding"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c9c64e06",
   "metadata": {},
   "outputs": [],
   "source": [
    "my $mat = $smrObj.take-matrices<year>;\n",
    "\n",
    "my $matUp = Math::SparseMatrix.new(\n",
    "    diagonal => 1/2 xx ($mat.columns-count - 1), k => 1, \n",
    "    row-names => $mat.column-names,\n",
    "    column-names => $mat.column-names\n",
    ");\n",
    "\n",
    "my $matDown = $matUp.transpose;\n",
    "\n",
    "# mat = mat + mat . matDown + mat . matDown\n",
    "$mat = $mat.add($mat.dot($matUp)).add($mat.dot($matDown));"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a8b2647d",
   "metadata": {},
   "outputs": [],
   "source": [
    "#%js\n",
    " my %opts = margins => {top => 30, left => 16, right => 16, bottom => 16}, :$tick-labels-font-size, :$tick-labels-color, :$title-color, :tooltip, :$tooltip-color, :$color-palette, :$tooltip-background-color, :$background;\n",
    "$mat[(^$mat.rows-count).pick(50).sort; 'year:' X~ (1970..2000)].Array\n",
    "==> js-d3-matrix-plot(:600width, :400height, |%opts)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1e631edf",
   "metadata": {},
   "outputs": [],
   "source": [
    "#% js\n",
    "js-d3-list-plot($mat.tuples, :$background, :600width, :500height, point-size => 1, :!axes)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8b1226ee",
   "metadata": {},
   "outputs": [],
   "source": [
    "my %matrices = $smrObj.take-matrices;\n",
    "%matrices<year> = $mat;\n",
    "my $smrObj2 = ML::SparseMatrixRecommender.new(%matrices)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "aea72184",
   "metadata": {},
   "source": [
    "### Recommendations"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9024f177",
   "metadata": {},
   "source": [
    "Example recommendation by profile:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "875be3d5",
   "metadata": {},
   "outputs": [],
   "source": [
    "sink $smrObj\n",
    ".apply-tag-type-weights({genre => 2})\n",
    ".recommend-by-profile(<genre:History year:1999>, 12, :!normalize)\n",
    ".join-across(select-columns(@dsMovieData, @field-names), 'index')\n",
    ".echo-value(as => {to-pretty-table($_, align => 'l', field-names => ['score', |@field-names])})"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cbf66927",
   "metadata": {},
   "source": [
    "Recommendation by history:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0450bfd4",
   "metadata": {},
   "outputs": [],
   "source": [
    "sink $smrObj\n",
    ".recommend(<2125 2308>, 12, :!normalize, :!remove-history)\n",
    ".join-across(select-columns(@dsMovieData, @field-names), 'index')\n",
    ".echo-value(as => {to-pretty-table($_, align => 'l', field-names => ['score', |@field-names])})"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "92b3a12f",
   "metadata": {},
   "source": [
    "### Profiles"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7beb6d40",
   "metadata": {},
   "source": [
    "Find movie IDs for a certain criteria (e.g. action movies):"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "585cf466",
   "metadata": {},
   "outputs": [],
   "source": [
    "my @movieIDs = $smrObj.recommend-by-profile('actor:Orlando Bloom', Inf).take-value».key;\n",
    "deduce-type(@movieIDs)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "368e4ac7",
   "metadata": {},
   "outputs": [],
   "source": [
    "my @profile = |$smrObj.profile(@movieIDs).take-value;\n",
    "deduce-type(@profile)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ee996cea",
   "metadata": {},
   "outputs": [],
   "source": [
    "outlier-identifier(@profile».value, identifier => &top-outliers o &quartile-identifier-parameters)\n",
    "==> {@profile[$_]}()\n",
    "==> my @profile2;\n",
    "\n",
    "deduce-type(@profile2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0bc63326",
   "metadata": {},
   "outputs": [],
   "source": [
    "#%js\n",
    "js-d3-list-plot(\n",
    "    [|@profile».value.kv.map(-> $x, $y { %(:$x, :$y, group => 'full profile' ) }), \n",
    "     |@profile2».value.kv.map(-> $x, $y { %(:$x, :$y, group => 'outliers' ) })], \n",
    "    :$background,\n",
    "    :300height,\n",
    "    :600width\n",
    "    )"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5f7b48aa",
   "metadata": {},
   "source": [
    "---\n",
    "\n",
    "## References"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "056360e9",
   "metadata": {},
   "source": [
    "### Articles, blog posts\n",
    "\n",
    "[AA1] Anton Antonov, [\"Introduction to data wrangling with Raku\"](https://rakuforprediction.wordpress.com/2021/12/31/introduction-to-data-wrangling-with-raku/), (2021), [RakuForPrediction at WordPress](https://rakuforprediction.wordpress.com).\n",
    "\n",
    "[AA2] Anton Antonov, [\"Implementing Machine Learning algorithms in Raku (TRC-2022 talk)\"](https://rakuforprediction.wordpress.com/2022/08/15/implementing-machine-learning-algorithms-in-raku-trc-2022-talk/), (2021), [RakuForPrediction at WordPress](https://rakuforprediction.wordpress.com)."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d7147d5d",
   "metadata": {},
   "source": [
    "### Notebooks \n",
    "\n",
    "[AAn1] Anton Antonov, \n",
    "[\"Small movie dataset analysis\"](), \n",
    "(2025),\n",
    "[RakuForPrediction-blog at GitHub]().\n",
    "\n",
    "[AAn2] Anton Antonov, \n",
    "[\"Small movie dataset recommender\"](), \n",
    "(2025),\n",
    "[RakuForPrediction-blog at GitHub]().\n",
    "\n",
    "[AAn3] Anton Antonov, \n",
    "[\"Small movie dataset graph\"](), \n",
    "(2025),\n",
    "[RakuForPrediction-blog at GitHub]().\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2f6c1972",
   "metadata": {},
   "source": [
    "### Packages\n",
    "\n",
    "[AAp1] Anton Antonov, [Data::Importers, Raku package](https://github.com/antononcube/Raku-Data-Importers), (2024-2025), [GitHub/antononcube](https://github.com/antononcube).\n",
    "\n",
    "[AAp2] Anton Antonov, [Data::Reshapers, Raku package](https://github.com/antononcube/Raku-Data-Reshapers), (2021-2025), [GitHub/antononcube](https://github.com/antononcube).\n",
    "\n",
    "[AAp3] Anton Antonov, [Data::Summarizers, Raku package](https://github.com/antononcube/Raku-Data-Summarizers), (2021-2024), [GitHub/antononcube](https://github.com/antononcube).\n",
    "\n",
    "[AAp4] Anton Antonov, [Graph, Raku package](https://github.com/antononcube/Raku-Graph), (2024-2025), [GitHub/antononcube](https://github.com/antononcube).\n",
    "\n",
    "[AAp5] Anton Antonov, [JavaScript::D3, Raku package](https://github.com/antononcube/Raku-JavaScript-D3), (2022-2025), [GitHub/antononcube](https://github.com/antononcube).\n",
    "\n",
    "[AAp6] Anton Antonov, [Jupyter::Chatbook, Raku package](https://github.com/antononcube/Raku-Jupyter-Chatbook), (2023-2025), [GitHub/antononcube](https://github.com/antononcube).\n",
    "\n",
    "[AAp7] Anton Antonov, [Math::SparseMatrix, Raku package](https://github.com/antononcube/Raku-Math-SparseMatrix), (2024-2025), [GitHub/antononcube](https://github.com/antononcube).\n",
    "\n",
    "[AAp8] Anton Antonov, [ML::AssociationRuleLearning, Raku package](https://github.com/antononcube/Raku-ML-AssociationRuleLearning), (2022-2024), [GitHub/antononcube](https://github.com/antononcube).\n",
    "\n",
    "[AAp9] Anton Antonov, [ML::SparseMatrixRecommender, Raku package](https://github.com/antononcube/Raku-ML-SparseMatrixRecommender), (2025), [GitHub/antononcube](https://github.com/antononcube).\n",
    "\n",
    "[AAp10] Anton Antonov, [Statistics::OutlierIdentifiers, Raku package](https://github.com/antononcube/Raku-Statistics-OutlierIdentifiers), (2022), [GitHub/antononcube](https://github.com/antononcube).\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "daf8dbd7",
   "metadata": {},
   "source": [
    "### Videos\n",
    "\n",
    "[AAv1] Anton Antonov, [\"Simplified Machine Learning Workflows Overview (Raku-centric)\"](https://www.youtube.com/watch?v=p3iwPsc6e74), (2022), [YouTube/@AAA4prediction](https://www.youtube.com/@AAA4prediction).\n",
    "\n",
    "[AAv2] Anton Antonov, [\"TRC 2022 Implementation of ML algorithms in Raku\"](https://www.youtube.com/watch?v=efRHfjYebs4), (2022), [YouTube/@AAA4prediction](https://www.youtube.com/@AAA4prediction).\n",
    "\n",
    "[AAv3] Anton Antonov, [\"Exploratory Data Analysis with Raku\"](https://www.youtube.com/watch?v=YCnjMVSfT8w), (2024), [YouTube/@AAA4prediction](https://www.youtube.com/@AAA4prediction).\n",
    "\n",
    "[AAv4] Anton Antonov, [\"Raku RAG demo\"](https://www.youtube.com/watch?v=JHO2Wk1b-Og), (2024), [YouTube/@AAA4prediction](https://www.youtube.com/@AAA4prediction).\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "RakuChatbook",
   "language": "raku",
   "name": "raku"
  },
  "language_info": {
   "file_extension": ".raku",
   "mimetype": "text/x-raku",
   "name": "raku",
   "version": "6.d"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
